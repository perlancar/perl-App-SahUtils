#!perl

# AUTHORITY
# DATE
# DIST
# VERSION

use 5.010001;
use strict;
use warnings;

use Perinci::CmdLine::Any;

our %SPEC;

$SPEC{resolve_sah_schema} = {
    v => 1.1,
    summary => 'Resolve Sah schema',
    args => {
        schema => {
            schema => 'str*', # XXX perl::data
            req => 1,
            pos => 0,
        },
        merge_clause_sets => {
            schema => 'bool*',
            default => 1,
        },
        stop_after_no_merge_keys => {
            schema => 'bool*',
        },
        min_steps => {
            schema => 'uint*',
        },
    },
};
sub resolve_sah_schema {
    require Data::Dump::Color;
    require Data::Sah::Resolve;

    my %args = @_;

    my $sch;
    if ($args{schema} =~ /\A\w+(::\w+)*\*?\z/) {
        $sch = $args{schema};
    } else {
        eval "\$sch = $args{schema}";
        die if $@;
    }

    my $nsch = Data::Sah::Resolve::resolve_schema(
        {
            merge_clause_sets        => $args{merge_clause_sets},
            stop_after_no_merge_keys => $args{stop_after_no_merge_keys},
            (defined($args{min_steps}) ? (min_steps=>$args{min_steps}) : ()),
        },
        $sch);

    [200, "OK", Data::Dump::Color::dump($nsch) . "\n", {
        'cmdline.skip_format' => 1,
    }];
}

Perinci::CmdLine::Any->new(
    url => '/main/resolve_sah_schema',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

 % resolve-sah-schema '"int"'
 ["int", []]

 % resolve-sah-schema '"posint"' ; # will look Sah::Schema::posint up
 ["int", {min=>1}]

 % resolve-sah-schema '["posint", div_by=>2]'
 ["int", {min=>1}, {div_by=>2}]

 % resolve-sah-schema '["posint", "merge.delete.min"=>undef, div_by=>2]'
 ["int", {div_by=>2}]


=head1 DESCRIPTION

This script can be used to quickly produce/see the resolved form of a L<Sah>
schema. Both input and output are in the form of Perl code.


=head1 SEE ALSO

L<Data::Sah::Resolve>
