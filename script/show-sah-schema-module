#!perl

use 5.010001;
use strict 'subs', 'vars';
use warnings;
use Perinci::CmdLine::Any;

# AUTHORITY
# DATE
# DIST
# VERSION

our %SPEC;

$SPEC{app} = {
    v => 1.1,
    summary => 'Show schema from Sah::Schema module',
    args => {
        name => {
            schema => ['perl::modname*'],
            pos => 0,
            completion => sub {
                my %args = @_;
                require Complete::Module;
                Complete::Module::complete_module(
                    word => $args{word},
                    ns_prefix => 'Sah::Schema',
                );
            },
        },
        list => {
            schema => ['bool', is=>1],
            cmdline_aliases => {l=>{}},
        },
    },
};
sub app {
    my %args = @_;

    my $name = $args{name};

    if ($args{list}) {
        my @res;
        require Module::List::Tiny;
        my $res = Module::List::Tiny::list_modules(
            'Sah::Schema::', {list_modules=>1, recurse=>1});
        for (sort keys %$res) {
            s/\ASah::Schema:://;
            push @res, $_;
        }
        return [200, "OK", \@res];
    } elsif ($name) {
        $name =~ /\A\w+(::\w+)*\z/
            or return [400, "Invalid module name '$name'"];;
        $name = "Sah::Schema::$name";
        (my $name_pm = "$name.pm") =~ s!::!/!g;
        require $name_pm;
        my $sch = ${"$name\::schema"};
        if ($args{-cmdline} &&
                ($args{-cmdline_r}{format} // 'text') =~ /text/) {
            require Data::Dump::Color;
            return [200, "OK", Data::Dump::Color::dump($sch) . "\n",
                    {'cmdline.skip_format'=>1}];
        } else {
            return [200, "OK", $sch];
        }
    } else {
        return [400, "Please specify name of a Sah schema module (or -l)"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/app',
    pass_cmdline_object => 1,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SEE ALSO

L<list-sah-schema-modules>
