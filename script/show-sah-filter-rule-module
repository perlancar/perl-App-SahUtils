#!perl

# AUTHORITY
# DATE
# DIST
# VERSION

use 5.010001;
use strict 'subs', 'vars';
use warnings;
use Perinci::CmdLine::Any;

our %SPEC;

$SPEC{app} = {
    v => 1.1,
    summary => 'Show Data::Sah::Filter::{,perl,js} module',
    args => {
        name => {
            schema => ['perl::modname*'],
            pos => 0,
            completion => sub {
                my %args = @_;
                require Complete::Module;
                require Complete::Util;
                Complete::Util::combine_answers(
                    Complete::Module::complete_module(
                        word => $args{word},
                        ns_prefix => 'Data::Sah::Filter',
                    ),
                    Complete::Module::complete_module(
                        word => $args{word},
                        ns_prefix => 'Data::Sah::Filter::perl',
                    ),
                    Complete::Module::complete_module(
                        word => $args{word},
                        ns_prefix => 'Data::Sah::Filter::js',
                    ),
                );
            },
        },
        list => {
            schema => ['bool', is=>1],
            cmdline_aliases => {l=>{}},
        },
    },
};
sub app {
    my %args = @_;

    my $name = $args{name};

    if ($args{list}) {
        my @res;
        require Module::List::Tiny;
        my $res = Module::List::Tiny::list_modules(
            'Data::Sah::Filter::', {list_modules=>1, recurse=>1});
        for (sort keys %$res) {
            s/\AData::Sah::Filter:://;
            push @res, $_;
        }
        return [200, "OK", \@res];
    } elsif ($name) {
        require File::Slurper;
        require Module::Path::More;
        $name =~ /\A\w+(::\w+)*\z/
            or return [400, "Invalid module name '$name'"];;
        my $found;
        for my $prefix ("Data::Sah::Filter", "Data::Sah::Filter::perl", "Data::Sah::Filter::js", "") {
            my $fullname = "$prefix\::$name";
            my $path = Module::Path::More::module_path(module => $fullname);
            next unless $path;
            $found++;
            return [200, "OK", File::Slurper::read_text($path)];
        }
        return [404, "No such filter module name '$name'"] unless $found;
    } else {
        return [400, "Please specify name of a Sah filter module (or -l)"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/app',
    pass_cmdline_object => 1,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SEE ALSO

L<list-sah-filter-rule-modules>
