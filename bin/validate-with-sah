#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any;

our %SPEC;

$SPEC{validate_with_sah} = {
    v => 1.1,
    summary => 'Validate data with Sah schema',
    description => <<'_',

Can also be used to just normalize a Sah schema and show it (see
`--show-schema`), or generate validator code and show it (see `--show-code`).

_
    args_groups => [
        {rel=>'one_of', args=>[qw/schema schema_file/]}, # XXX req_one_of
        {rel=>'one_of', args=>[qw/data multiple_data/]},
        # {rel=>'depends', args=>[qw/show_linenum/], on_args=>[qw/show_code/]},
    ],
    args => {
        schema => {
            schema=>'any*',
            pos=>0,
        },
        schema_file => {
            schema=>'str*',
            summary => 'Retrieve schema from file',
            description => <<'_',

JSON and YAML formats are supported. File type will be guessed from filename,
defaults to JSON.

_
            cmdline_aliases => {f=>{}},
            'x.schema.entity' => 'filename',
        },
        schema_file_type => {
            schema=>['str*', in=>[qw/json yaml/]],
            summary => 'Give hint for schema file type',
            cmdline_aliases => {t=>{}},
        },
        data => {
            schema => ['any'],
            pos => 1,
        },
        multiple_data => {
            schema => ['array*', of=>'any'],
        },
        return_type => {
            schema=>['str*', in=>[qw/bool str full/]],
            default=>'str',
            cmdline_aliases => {r=>{}},
        },
        show_schema => {
            summary => "Don't validate data, show normalized schema only",
            schema=>['bool', is=>1],
            cmdline_aliases => {s=>{}},
        },
        show_code => {
            summary => "Don't validate data, show generated validator code only",
            schema=>['bool', is=>1],
            cmdline_aliases => {c=>{}},
        },
        show_data_with_result => {
            summary => "Show data alongside with validation result",
            description => <<'_',

The default is to show the validation result only.

_
            schema=>['bool', is=>1],
            cmdline_aliases => {d=>{}},
        },
        compiler => {
            summary => "Select compiler",
            schema=>['str*', in=>[qw/perl js/]],
            default => 'perl',
            cmdline_aliases => {C=>{}},
        },
        show_linenum => {
            summary => 'When showing source code, add line number',
            schema=>['bool', is=>1],
            cmdline_aliases => {l=>{}},
        },
    },
    examples => [
        {
            src => q([[prog]] 'int*' 42),
            src_plang => 'bash',
            summary => 'Should succeed and return empty string',
        },
        {
            src => q([[prog]] 'int*' '"x"'),
            src_plang => 'bash',
            summary => 'Should show an error message because "x" is not int',
        },
        {
            src => q([[prog]] '["int","min",1,"max",10]' --multiple-data-json '[-4,7,15]' --return-type bool),
            src_plang => 'bash',
            summary => 'Validate multiple data, should return 0, 1, 0',
        },
        {
            src => q([[prog]] '["int","min",1,"max",10]' --multiple-data-json '[-4,7,15]' -d),
            src_plang => 'bash',
            summary => 'Show data alongside with result, in a table',
        },
        {
            src => q([[prog]] '["int","min",1,"max",10]' -c -l),
            src_plang => 'bash',
            summary => 'Show validator Perl code only, with line number',
        },
        {
            src => q([[prog]] '["int","min",1,"max",10]' -C js -c -l),
            src_plang => 'bash',
            summary => 'Show validator JS code only, with line number',
        },
        {
            src => q{[[prog]] -f schema1.json '["data"]'},
            src_plang => 'bash',
            summary => 'Load schema from file',
        },
    ],
};
sub validate_with_sah {
    my %args = @_;

    my $schema;
    if (defined $args{schema}) {
        return [400, "Please specify either 'schema' or 'schema_file', not both"] if defined($args{schema_file});
        $schema = $args{schema};
    } elsif (defined $args{schema_file}) {
        my $path = $args{schema_file};
        my $type = $args{schema_file_type};
        if (!$type) {
            if ($path =~ /\b(json)$/i) { $type = 'json' }
            elsif ($path =~ /\b(yaml|yml)$/i) { $type = 'yaml' }
            else { $type = 'json' }
        }
        if ($type eq 'json') {
            require File::Slurper;
            require JSON;
            my $ct = File::Slurper::read_text($path);
            $schema = JSON::decode_json($ct);
        } elsif ($type eq 'yaml') {
            require YAML::XS;
            $schema = YAML::XS::Load($path);
        } else {
            return [400, "Unknown schema file type '$type', please specify json/yaml"];
        }
    } else {
        return [400, "Please specify 'schema' or 'schema_file'"];
    }

    if ($args{show_schema}) {
        require Data::Sah::Normalize;
        return [200, "OK", Data::Sah::Normalize::normalize_schema($schema)];
    }

    my $func;
    {
        no strict 'refs';
        my $c = $args{compiler};
        if ($c eq 'perl') {
            require Data::Sah;
            $func = \&{"Data::Sah::gen_validator"};
        } elsif ($c eq 'js') {
            require Data::Sah::JS;
            $func = \&{"Data::Sah::JS::gen_validator"};
        } else {
            return [400, "Unknown compiler '$c', please specify perl/js"];
        }
    }

    my $v;
    {
        my %opts;
        if ($args{show_code}) { $opts{source} = 1 }
        $opts{return_type} = $args{return_type};
        $v = $func->($schema, \%opts);
    }

    if ($args{show_code}) {
        $v .= "\n" unless $v =~ /\R\z/;
        if ($args{show_linenum}) {
            require String::LineNumber;
            $v = String::LineNumber::linenum($v);
        }
        return [200, "OK", $v, {'cmdline.skip_format'=>1}];
    }

    if (exists $args{data}) {
        if ($args{show_data_with_result}) {
            return [200, "OK", {data=>$args{data}, result=>$v->($args{data})}];
        } else {
            return [200, "OK", $v->($args{data})];
        }
    } elsif ($args{multiple_data}) {
        if ($args{show_data_with_result}) {
            return [200, "OK", [map {{data=>$_, result=>$v->($_)}} @{ $args{multiple_data} }]];
        } else {
            return [200, "OK", [map {$v->($_)} @{ $args{multiple_data} }]];
        }
    } else {
        return [400, "Please specify 'data' or 'multiple_data'"];
    }

    [200, "OK"];
}

my $cli = Perinci::CmdLine::Any->new(
    url => '/main/validate_with_sah',
);
$cli->{common_opts}{naked_res}{default} = 1;
$cli->run;

# ABSTRACT:
# PODNAME:

